/**
 * Module:	vm_options_proposal.ycp
 *
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * Purpose:     Proposal for virtual machine options
 *
 * $Id:$
 *
 */
{
    textdomain "vm";

    import "VM";

    string func = (string) WFM::Args (0);
    map param = (map) WFM::Args (1);
    map ret = $[];

    if ( func == "MakeProposal" )
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	if (force_reset)
	{
	    VM::ResetMemorySize();
	    VM::ResetStartMode();
	    VM::ResetConfigName();
	    VM::ResetExtraArgs();
	}

	return VM::getOptionsProposal();
    }
    else if ( func == "AskUser" )
    {
	boolean has_next = param["has_next"]:false;

	symbol result = (symbol) WFM::CallFunction ("inst_vm_options",
						    [true, has_next]);

	ret = $[ "workflow_sequence" : result ];
    }
    else if ( func == "Description" )
    {
	ret =
	    $[
	      // this is a heading
	      "rich_text_title"	:	_("Options"),
	      // this is a menu entry
	      "menu_title"	:	_("&Options"),
	      "id"		:	"vm_options_stuff"
	    ];
    }

    return ret;
}
