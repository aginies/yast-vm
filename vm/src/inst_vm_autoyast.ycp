/**
 * Module:	inst_vm_network.ycp
 *
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * Purpose:	Ask user for network settings.
 *
 * $Id:$
 *
 */

{
    textdomain "vm";

    import "VM";
    import "Wizard";
    import "Popup";
    import "Report";
    import "Label";

    // screen title for network options
    string title = _("AutoYast Profile");

    // build and show dialog
    Wizard::OpenAcceptDialog();


    term contents = `HBox(
	`HSpacing(1),
	`TextEntry(`id(`profile), `opt(`hstretch), _("AutoYast &Profile"), VM::GetAutoYastProfile()),
	`HSpacing(1),
	`VBox(
	    `Label(""),
	    `PushButton(`id(`select_profile), _("Select Profile..."))
	),
	`HSpacing(1)
    );

    // help text for autoyast settings (1/3)
    string help_text = _("<p><b>AutoYast Profile Setup</b><br></p>")
	// help text for autoyast settings (2/3)
	+ _("<p>It is possible to install the virtual machine using AutoYast.
You can set all installation options in AutoYast and store them into a profile
file that can be used later for automatic installation.</p>
")
	// help text for autoyast settings (3/3)
	+ _("<p>If the profile contains configuration of a virtual machine
(CPU, memory, virtual disk configuration, etc.) it can be loaded and
used instead of the current configuration.</p>
");

    Wizard::SetContents (title, contents, help_text, true, true);

    symbol ret = nil;

    while (true)
    {
	ret = (symbol) Wizard::UserInput();

	if (ret == `abort && Popup::ConfirmAbort(`painless))
	{
	    break;
	}
	else if (ret == `cancel || ret == `back)
	{
	    break;
	}
	else if (ret == `select_profile)
	{
	    string new_profile = (string) UI::QueryWidget (`id(`profile), `Value);
	    new_profile = UI::AskForExistingFile(new_profile, "*.xml", _("Select an AutoYast Profile"));

	    if (new_profile != nil)
	    {
		UI::ChangeWidget(`id(`profile), `Value, new_profile);
	    }
	}
	else if (ret == `next)
	{
	    string profile = (string) UI::QueryWidget (`id(`profile), `Value);
	    VM::SetAutoYastProfile(profile);

	    if (profile != "")
	    {
		import "Profile";

		if (!Profile::ReadXML(profile))
		{
		    Report::Warning(sformat(_("Cannot load profile '%1'
in the current system. It may be broken."), profile));
		}
		else
		{
		    y2milestone("Loaded AutoYast profile: %1", Profile::current);
		    // import VM config

		    map xen_setting = Profile::current["xen"]:$[];

		    if (xen_setting != nil && xen_setting != $[] &&
			// ask user to load VM settings from the profile
			Popup::YesNo(_("Load the virtual machine configuration from the profile?")))
		    {
			y2milestone("Import settings: %1", xen_setting);
			VM::Import(xen_setting);
		    }
		}
	    }

	    break;
	}
    }

    Wizard::CloseDialog();

    return ret;
}
