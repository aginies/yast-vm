/**
 * Module:	xen_mode_proposal.ycp
 *
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * Purpose:     Proposal for installation source configuration
 *
 * $Id$
 *
 */
{
    textdomain "vm";

    import "VM";
    import "VM_Common";

    import "Report";
    Report::DisplayErrors(true, 0);

    string func = (string) WFM::Args (0);
    map param = (map) WFM::Args (1);
    map ret = $[];

    if ( func == "MakeProposal" )
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	if (force_reset)
	{
	    VM::ResetVirtualizationType();
	}

	ret = $[ "preformatted_proposal" : VM::GetVirtualizationProposal() ];
    }
    else if ( func == "AskUser" )
    {
	boolean has_next = param["has_next"]:false;

	symbol result = (symbol) WFM::CallFunction ("inst_xen_mode",
						    [true, has_next]);

	ret = $[
		"workflow_sequence"	: result,
		"mode_changed"		: true  // force inst_proposal to regenerate the rich_text_title and menu_title for each section
	];
    }
    else if ( func == "Description" )
    {
	if (VM_Common::proposal_type == "boot") return $[]; // no useful proposal for this proposal_type
	else {
		ret = $[
			// this is a heading
			"rich_text_title"	: _("Virtualization Mode"),
			// this is a menu entry
			"menu_title"		: _("Virtualization Mode"),
			"id"			: "xen_mode"
		    ];
	}
    }

    return ret;
}
