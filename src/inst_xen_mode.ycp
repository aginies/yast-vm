/**
 * Module:	inst_vm_network.ycp
 *
 * Authors:	Ladislav Slezak <lslezak@suse.cz>
 *
 * Purpose:	Ask user for network settings.
 *
 * $Id$
 *
 */

{
    textdomain "vm";

    import "VM";
    import "VM_Common";
    import "Wizard";
    import "Popup";
    import "Report";
    import "Label";
    import "Mode";


    // screen title for the Xen mode selection
    string title = _("Virtualization Mode");

    // build and show dialog
    Wizard::OpenAcceptDialog ();

    term contents = `HBox(
	`HSpacing(2),
	`RadioButtonGroup(`id(`rb_group),
	    `Frame( title,
		`VBox(
		    `VSpacing(0.5),
		    `Left(`RadioButton(`id("para"), _("Paravirtualization (Requires Modified Guest OS)"), VM::GetVirtualizationType() == "para")),
		    `VSpacing(0.5),
		    `Left(`RadioButton(`id("full"), _("Full Virtualization (Unmodified Guest OS)"), VM::GetVirtualizationType() == "full")),
		    `VSpacing(0.5)
		)
	    )
	),
	`HSpacing(2)
    );

    // help text for virtualization mode setting (1/2)
    string help_text = _("<p><b><big>Virtualization Mode</big></b><br></p>")
	// help text for virtualization mode setting (2/2)
	+ _("<p>Xen supports two possible virtualization modes:
paravirtualization and full virtualization.</p>")

	// help text for virtualization mode setting (3/3)
	+ _("<p><b>Paravirtualization</b> requires a modified guest OS that
supports Xen virtualization.</p>
")

	// help text for virtualization mode setting (4/4)
	+ _("<p><b>Full virtualization</b> requires CPU with virtualization
support. In this case, Xen completely emulates PC hardware in a virtual machine.
It is possible to install an unmodified guest OS.</p>
")

	// help text for virtualization mode setting (5/5)
	+ _("<p>Not every option is available in each mode. Changing the mode will cause some settings to be reset to default values.</p>");

    Wizard::SetContents (title, contents, help_text, true, true);

    // set full virtualization widget state
    if (!Mode::config() && !VM_Common::VirtualizationCPU())
    {
	// not autoyast config mode and no virtualization CPU => disable full virtualization
	UI::ChangeWidget(`id("full"), `Enabled, false);
    }

    symbol ret = nil;

    while (true)
    {
	ret = (symbol) Wizard::UserInput();

	if (ret == `abort && Popup::ConfirmAbort(`painless))
	    break;

	if (ret == `cancel || ret == `back)
	    break;

	if (ret == `next)
	{
	    string selected = (string)(UI::QueryWidget(`id(`rb_group), `CurrentButton));

	    if (selected != VM::GetVirtualizationType()) {
		VM_Common::SetModified(true);
		VM::SetVirtualizationType(selected);
		VM::ResetAllSettings();
	    }

	    break;
	}
    }

    Wizard::CloseDialog();

    return ret;
}
