/**
 * Module:	vm_network_proposal.ycp
 *
 * Author:	Ladislav Slezak <lslezak@suse.cz>
 *
 * Purpose:     Proposal for network configuration
 *
 * $Id:$
 *
 */

{
    textdomain "vm";

    import "VM";
    import "HTML";

    string func = (string) WFM::Args (0);
    map param = (map) WFM::Args (1);
    map ret = $[];

    if (func == "MakeProposal")
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	if (force_reset)
	{
	    // reset user settings and propose new values
	    VM::ResetNetworkProposal();
	}

	ret = $[ "preformatted_proposal" : HTML::List(VM::GetNetworkProposal()) ];

	map warn = VM::GetNetworkProposalWarning();
	if (warn != nil && size(warn) > 0)
	{
	    ret = union(ret, warn);
	}
    }
    else if (func == "AskUser")
    {
	boolean has_next = param["has_next"]:false;
	symbol result = (symbol) WFM::CallFunction ("inst_vm_network",
						    [true, has_next]);
	ret = $[ "workflow_sequence" : result ];
    }
    else if (func == "Description")
    {
	ret =  $[
	  // this is a heading
	  "rich_text_title"	:	_("Network"),
	  // this is a menu entry
	  "menu_title"	:	_("&Network"),
	  "id"		:	"vm_network_setting"
	];
    }

    return ret;
}
